---
title: 'Berachainã§ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½ãªERC20ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ [Berachainç¿»è¨³]'
tags:
  - Blockchain
  - Ethereum
  - Web3
  - Berachain
  - SunriseLayer
private: false
updated_at: '2024-09-12T00:40:33+09:00'
id: f53b7914c1ce3b3a0650
organization_url_name: sunriselayer
slide: false
ignorePublish: false
---
:::note info
æœ¬è¨˜äº‹ã¯ä¸‹è¨˜ã®ç¿»è¨³ã¨ãªã‚Šã¾ã™ã€‚
[ã€Deploy an Upgradeable ERC20 Token on Berachainã€](https://blog.berachain.com/blog/deploy-an-upgradeable-erc20-token-on-berachain-2)
:::

![image.png](https://qiita-image-store.s3.ap-northeast-1.amazonaws.com/0/1926720/57fecd18-7b3e-1331-0512-a176784fce7b.png)

## ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ãªã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ç´¹ä»‹

Berachain ã®ã‚ˆã†ãªãƒ–ãƒ­ãƒƒã‚¯ãƒã‚§ãƒ¼ãƒ³ä¸Šã®ã‚¹ãƒãƒ¼ãƒˆã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¯ã€ä¸€åº¦ãƒ‡ãƒ—ãƒ­ã‚¤ã•ã‚Œã‚‹ã¨é€šå¸¸ã¯ä¸å¤‰ã§ã™ã€‚ã“ã‚Œã¯ç¢ºå®Ÿæ€§ã‚’æä¾›ã—ã¾ã™ãŒã€ãƒã‚°ã®ä¿®æ­£ã€æ©Ÿèƒ½ã®è¿½åŠ ã€ãã—ã¦æ€¥é€Ÿã«å¤‰åŒ–ã™ã‚‹ç’°å¢ƒã«é©å¿œã™ã‚‹ãŸã‚ã«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’æ›´æ–°ã™ã‚‹å¿…è¦ãŒã‚ã‚‹é–‹ç™ºè€…ã«ã¨ã£ã¦ã¯èª²é¡Œã¨ãªã‚‹å¯èƒ½æ€§ãŒã‚ã‚Šã¾ã™ã€‚ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ãªã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¯ã€ä¸å¤‰æ€§ã¨æŸ”è»Ÿæ€§ã®é–“ã®è§£æ±ºç­–ã‚’æä¾›ã—ã¾ã™ã€‚

### Proof-of-Liquidity ã¨ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«æ€§

Berachain ã®æ–°ã—ã„ Proof-of-Liquidityï¼ˆPoLï¼‰ã‚³ãƒ³ã‚»ãƒ³ã‚µã‚¹ãƒ¡ã‚«ãƒ‹ã‚ºãƒ ã«å‚åŠ ã™ã‚‹ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã¯ã€é€šå¸¸ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ã« Berachain ã®ãƒã‚¤ãƒ†ã‚£ãƒ–ãƒˆãƒ¼ã‚¯ãƒ³`$BGT`ã®å ±é…¬ã‚’ç²å¾—ã™ã‚‹ãŸã‚ã«ã€ãã®ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã¸ã®é é‡‘ã‚’è¡¨ã™**ERC20 ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°**ã™ã‚‹ã“ã¨ã‚’è¦æ±‚ã—ã¾ã™ã€‚
https://docs.berachain.com/learn/what-is-proof-of-liquidity?ref=berachain.ghost.io

ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã¯æœ€åˆã«ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ãƒ¢ãƒ‡ãƒ«ã‹ã‚‰å§‹ã¾ã‚‹ã‹ã‚‚ã—ã‚Œã¾ã›ã‚“ãŒã€ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã®æ©Ÿèƒ½ã‚’å¤‰æ›´ã›ãšã«å ±é…¬ã«å°‘ã—å‰µé€ æ€§ã‚’åŠ ãˆãŸã„å ´åˆã¯ã©ã†ã§ã—ã‚‡ã†ã‹ï¼Ÿã“ã“ã§ã¯ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ãªã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆãŒã©ã®ã‚ˆã†ã«ã—ã¦ã“ã‚Œã‚’å¯èƒ½ã«ã™ã‚‹ã‹ã‚’æ¢ã‚Šã¾ã™ã€‚

> ğŸ’¡ ãƒˆãƒ¼ã‚¯ãƒ³ã®ç§»è¡Œãªã—ã« PoL ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ãƒ¡ã‚«ãƒ‹ã‚ºãƒ ã‚’å¤‰æ›´ã§ãã¾ã™

### ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚¬ã‚¤ãƒ‰ - æ¦‚è¦

ã“ã®ã‚¬ã‚¤ãƒ‰ã§ã¯ã€[Foundry](https://github.com/foundry-rs/foundry?ref=berachain.ghost.io)ã¨[OpenZeppelin ã®ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆ](https://docs.openzeppelin.com/upgrades-plugins/1.x/foundry-upgrades?ref=berachain.ghost.io)ã‚’ä½¿ç”¨ã—ã¦ã€Berachain ä¸Šã§ ERC20 ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ä½œæˆã€ãƒ‡ãƒ—ãƒ­ã‚¤ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã™ã‚‹ãƒ—ãƒ­ã‚»ã‚¹ã‚’èª¬æ˜ã—ã¾ã™ã€‚å…·ä½“çš„ã«ã¯ã€ä»¥ä¸‹ã®ã“ã¨ã‚’é”æˆã—ã¾ã™ï¼š

1. ERC20 ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆï¼ˆ`v1 Implementation`ï¼‰ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹
2. `v1 Implementation`ã®ãƒ­ã‚¸ãƒƒã‚¯ã‚’ç¶™æ‰¿ã™ã‚‹ Proxy ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹
3. PoL å ±é…¬ã®æ™‚é–“ãƒ™ãƒ¼ã‚¹ã®ãƒ–ãƒ¼ã‚¹ãƒˆã‚’å®Ÿè£…ã™ã‚‹ãŸã‚ã®æ–°ã—ã„é–¢æ•°ã‚’æŒã¤ã€ä¿®æ­£ã•ã‚ŒãŸ ERC20 ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆï¼ˆ`v2 Implementation`ï¼‰ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹
4. Proxy ã‚’`v2 Implementation`ã®ãƒ­ã‚¸ãƒƒã‚¯ã‚’ä½¿ç”¨ã™ã‚‹ã‚ˆã†ã«ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã™ã‚‹

### ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚¹ãƒãƒ¼ãƒˆã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ä»•çµ„ã¿

ã€ŒProxyã€ã‚„ã€Œå®Ÿè£…ã€ã¨ã„ã†è¨€è‘‰ã¯è€³æ…£ã‚Œãªã„ã‹ã‚‚ã—ã‚Œã¾ã›ã‚“ã€‚ãã“ã§ã€ã‚³ãƒ¼ãƒ‰ã«å…¥ã‚‹å‰ã«ã„ãã¤ã‹ã®ç”¨èªã¨æ¦‚å¿µã‚’æ˜ç¢ºã«ã—ã¦ãŠãã¾ã—ã‚‡ã†ã€‚

- **`Proxy`**ã¯ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ãŒç›´æ¥ã‚„ã‚Šå–ã‚Šã™ã‚‹ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã§ã™ã€‚ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ãƒ‡ãƒ¼ã‚¿ã¨çŠ¶æ…‹ã‚’ä¿å­˜ã™ã‚‹å½¹å‰²ã‚’æ‹…ã„ã¾ã™ã€‚ã—ã‹ã—ã€ã“ã‚Œã¯å˜ãªã‚‹å¤–æ®»ã§ã‚ã‚Šã€æ©Ÿèƒ½ã‚„ãƒ­ã‚¸ãƒƒã‚¯ã¯å«ã¾ã‚Œã¦ã„ã¾ã›ã‚“ã€‚ãã‚Œã¯ä»¥ä¸‹ã®å½¹å‰²ã§ã™...

**`Implementation`**ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã€‚`Implementation`ã¯ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼å‘ã‘ã®`Proxy`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ãŸã‚ã®ã™ã¹ã¦ã®ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆãƒ­ã‚¸ãƒƒã‚¯ã‚’ãƒ›ã‚¹ãƒˆã—ã¾ã™ãŒã€ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚¢ãƒ‰ãƒ¬ã‚¹ã«ãƒ‡ãƒ¼ã‚¿ã‚’ä¿å­˜ã™ã‚‹ã“ã¨ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚

![](https://super-translator.inaridiy.workers.dev/assets/image/288eee44-5b66-4e90-803e-7aee22e33e8d)

ä¸Šã®å›³ã§ç¤ºã•ã‚Œã¦ã„ã‚‹ã‚ˆã†ã«ã€`Proxy`ã¨`Implementation`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¯é€£æºã—ã¦å‹•ä½œã—ã¾ã™ï¼š

- ãƒ¦ãƒ¼ã‚¶ãƒ¼ã¯ã¾ãš`Proxy`ã«`call`ã‚’è¡Œã„ã¾ã™ã€‚
- ãƒªã‚¯ã‚¨ã‚¹ãƒˆã¯`delegatecall`ã‚’ä½¿ç”¨ã—ã¦é–¢é€£ã™ã‚‹`Implementation`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«ãƒ«ãƒ¼ãƒ†ã‚£ãƒ³ã‚°ã•ã‚Œã¾ã™ã€‚
- `Proxy`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®è¨±å¯ã•ã‚ŒãŸæ‰€æœ‰è€…ã¯ã€ç•°ãªã‚‹`Implementation`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆé–“ã§åˆ‡ã‚Šæ›¿ãˆã‚‹ã“ã¨ãŒã§ãã¾ã™ - ã¤ã¾ã‚Šã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½ã§ã™ï¼

ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«ã¯æ§˜ã€…ãªç¨®é¡ãŒã‚ã‚Šã¾ã™ã€‚ã“ã®ãƒãƒ¥ãƒ¼ãƒˆãƒªã‚¢ãƒ«ã§ä½¿ç”¨ã•ã‚Œã‚‹ã‚¿ã‚¤ãƒ—ã¯**UUPS**ï¼ˆUniversal Upgradeable Proxy Standardï¼‰ã§ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ãƒ­ã‚¸ãƒƒã‚¯ã‚’`Implementation`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆè‡ªä½“ã«çµ„ã¿è¾¼ã‚“ã§ã„ã¾ã™ã€‚ã“ã®è¨­è¨ˆã¯ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®æ§‹é€ ã‚’ç°¡ç´ åŒ–ã—ã€`Proxy`ã®ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã‚’ç®¡ç†ã™ã‚‹ãŸã‚ã®è¿½åŠ ã®ç®¡ç†ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®å¿…è¦æ€§ã‚’æ’é™¤ã—ã¾ã™ã€‚ç•°ãªã‚‹ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«ã¤ã„ã¦è©³ã—ãå­¦ã¶ã«ã¯ã€ã“ã®[OpenZeppelin ã‚¬ã‚¤ãƒ‰](https://docs.openzeppelin.com/contracts/5.x/api/proxy?ref=berachain.ghost.io)ã‚’å‚ç…§ã—ã¦ãã ã•ã„ã€‚

## ğŸ“‹ è¦ä»¶

- Node `v20.11.0`ä»¥ä¸Š
- [pnpm](https://pnpm.io/installation?ref=berachain.ghost.io)
- [**Berachain bArtio ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯**](https://docs.berachain.com/developers/network-configurations?ref=berachain.ghost.io)ã§è¨­å®šã•ã‚ŒãŸã‚¦ã‚©ãƒ¬ãƒƒãƒˆ
- ãã®ã‚¦ã‚©ãƒ¬ãƒƒãƒˆã«`$BERA`ã¾ãŸã¯ Berachain ãƒ†ã‚¹ãƒˆãƒãƒƒãƒˆãƒˆãƒ¼ã‚¯ãƒ³ â€” [**_Berachain Faucet_**](https://bartio.faucet.berachain.com/?ref=berachain.ghost.io)ã‚’å‚ç…§

### Foundry ã®ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«

ã“ã®ã‚¬ã‚¤ãƒ‰ã§ã¯ Foundry ã®ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ãŒå¿…è¦ã§ã™ã€‚ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ã§ä»¥ä¸‹ã‚’å®Ÿè¡Œã—ã¦ãã ã•ã„ï¼š

```terminal
curl -L https://foundry.paradigm.xyz | bash;

foundryup;
# foundryup installs the 'forge' and 'cast' binaries, used later
```

ã‚ˆã‚Šè©³ç´°ãªã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«æ‰‹é †ã«ã¤ã„ã¦ã¯ã€Foundry ã®[ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã‚¬ã‚¤ãƒ‰](https://book.getfoundry.sh/getting-started/installation?ref=berachain.ghost.io)ã‚’å‚ç…§ã—ã¦ãã ã•ã„ã€‚Berachain ã§ã® Foundry ã®ä½¿ç”¨ã«ã¤ã„ã¦ã®è©³ç´°ã¯ã€ã“ã®[ã‚¬ã‚¤ãƒ‰](https://docs.berachain.com/developers/guides/create-erc20-contract-using-foundry?ref=berachain.ghost.io)ã‚’å‚ç…§ã—ã¦ãã ã•ã„ã€‚

## ğŸ‘¨â€ğŸ’» ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã®ä½œæˆ

### ã‚¹ãƒ†ãƒƒãƒ— 1ï¼šãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã®ã‚»ãƒƒãƒˆã‚¢ãƒƒãƒ—

Foundry ã‚’åˆæœŸåŒ–ã—ã¦ï¼ˆæ–°ã—ã„ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆãƒ•ã‚©ãƒ«ãƒ€ã‚’ä½œæˆã—ã¦ï¼‰é–‹ç™ºç’°å¢ƒã‚’ã‚»ãƒƒãƒˆã‚¢ãƒƒãƒ—ã—ã¾ã—ã‚‡ã†ï¼š

```terminal
forge init pol-upgrades --no-git --no-commit;

cd pol-upgrades;
# We observe the following basic layout
# .
# â”œâ”€â”€ foundry.toml
# â”œâ”€â”€ script
# â”‚   â””â”€â”€ Counter.s.sol
# â”œâ”€â”€ src
# â”‚   â””â”€â”€ Counter.sol
# â””â”€â”€ test
#     â””â”€â”€ Counter.t.sol
```

æ—¢å­˜ã® Solidity ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ã™ã¹ã¦å‰Šé™¤ã—ã¾ã™ï¼š

```terminal
# FROM: ./pol-upgrades

rm script/Counter.s.sol src/Counter.sol test/Counter.t.sol;
```

ãã‚Œã§ã¯ã€å¿…è¦ãª OpenZeppelin ã®ä¾å­˜é–¢ä¿‚ã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã—ã¾ã™ï¼š

```terminal
# FROM: ./pol-upgrades


forge install OpenZeppelin/openzeppelin-contracts openzeppelin-contracts-upgradeable OpenZeppelin/openzeppelin-foundry-upgrades foundry-rs/forge-std --no-commit --no-git;
```

### ã‚¹ãƒ†ãƒƒãƒ— 2ï¼šFoundry ã®è¨­å®š

ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã®ãƒ«ãƒ¼ãƒˆã«`remappings.txt`ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®å†…å®¹ã‚’è¨˜è¿°ã—ã¾ã™ï¼š

```text
@openzeppelin/contracts/=lib/openzeppelin-contracts-upgradeable/lib/openzeppelin-contracts/contracts/
@openzeppelin/contracts-upgradeable/=lib/openzeppelin-contracts-upgradeable/contracts/
```

æ¬¡ã«ã€`foundry.toml`ãƒ•ã‚¡ã‚¤ãƒ«ã‚’æ›´æ–°ã—ã¾ã™ï¼š

```toml
[profile.default]
ffi = true
ast = true
build_info = true
evm_version = "cancun"
libs = ["lib"]
extra_output = ["storageLayout"]
```

### ã‚¹ãƒ†ãƒƒãƒ— 3ï¼šåˆæœŸãƒˆãƒ¼ã‚¯ãƒ³ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ä½œæˆ

`src/DeFiTokenV1.sol`ã¨ã„ã†ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®å†…å®¹ã‚’è¨˜è¿°ã—ã¾ã™ï¼š

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import "@openzeppelin/contracts-upgradeable/token/ERC20/ERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";

contract DeFiToken is ERC20Upgradeable, OwnableUpgradeable, UUPSUpgradeable {
    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    function initialize(address initialOwner) public initializer {
        __ERC20_init("DeFi Token", "DFT");
        __Ownable_init(initialOwner);
        __UUPSUpgradeable_init();

        _mint(initialOwner, 1000000 * 10 ** decimals());
    }

    function mint(address to, uint256 amount) external onlyOwner {
        _mint(to, amount);
    }

    function _authorizeUpgrade(
        address newImplementation
    ) internal override onlyOwner {}
}
```

ã“ã®ã‚¹ãƒãƒ¼ãƒˆã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«ã¯ã€ä»–ã®å ´æ‰€ã§ã¯è¦‹ã‚‰ã‚Œãªã„ã„ãã¤ã‹ã®ç‰¹å¾´ãŒã‚ã‚‹ã“ã¨ã«æ°—ã¥ãã§ã—ã‚‡ã†ï¼š

- ãƒ—ãƒ­ã‚­ã‚·åŒ–ã•ã‚ŒãŸã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¯**ã‚³ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚¿**ã‚’ä½¿ç”¨ã—ãªã„ãŸã‚ã€ã‚³ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚¿ã®ãƒ­ã‚¸ãƒƒã‚¯ã¯**`initialize`**é–¢æ•°ã«ç§»å‹•ã•ã‚Œã¦ã„ã¾ã™ã€‚ã“ã‚Œã¯ã€ãƒˆãƒ¼ã‚¯ãƒ³åã‚„æ‰€æœ‰è€…ã®è¨­å®šãªã©ã€é€šå¸¸ã®ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ã‚³ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚¿ã§å®Ÿè¡Œã•ã‚Œã‚‹é–¢æ•°ã‚’å®Ÿè¡Œã—ã¾ã™ã€‚
- ç¶™æ‰¿ã—ã¦ã„ã‚‹`ERC20`ã¨`Ownable`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¯ç‰¹åˆ¥ãªã€Œã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã€ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã§ã€ï¼ˆã‚³ãƒ³ã‚¹ãƒˆãƒ©ã‚¯ã‚¿å¤–ã§ã®ï¼‰åˆæœŸåŒ–ã‚„ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰æ™‚ã®å†åˆæœŸåŒ–ã‚’å®¹æ˜“ã«ã—ã¾ã™ã€‚

### ã‚¹ãƒ†ãƒƒãƒ— 4ï¼šãƒ‡ãƒ—ãƒ­ã‚¤ãƒ¡ãƒ³ãƒˆã‚¹ã‚¯ãƒªãƒ—ãƒˆã®ä½œæˆ

`script/DeployProxy.s.sol`ã¨ã„ã†ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®å†…å®¹ã‚’è¨˜è¿°ã—ã¾ã™ï¼š

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import "../src/DeFiTokenV1.sol";
import "forge-std/Script.sol";
import {Upgrades} from "openzeppelin-foundry-upgrades/Upgrades.sol";

contract DeployProxy is Script {
    function run() public {
        vm.startBroadcast();

        address proxy = Upgrades.deployUUPSProxy(
            "DeFiTokenV1.sol:DeFiToken",
            abi.encodeCall(DeFiToken.initialize, (msg.sender))
        );

        vm.stopBroadcast();

        console.log("Proxy Address:", address(proxy));
        console.log("Token Name:", DeFiToken(proxy).name());
    }
}
```

### ã‚¹ãƒ†ãƒƒãƒ— 5ï¼šç’°å¢ƒå¤‰æ•°ã®è¨­å®š

ãƒ‡ãƒ—ãƒ­ã‚¤ã«é€²ã‚€å‰ã«ã€ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã®ãƒ«ãƒ¼ãƒˆã«`.env`ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ã‚¦ã‚©ãƒ¬ãƒƒãƒˆã®ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆã‚­ãƒ¼ã‚’è¿½åŠ ã—ã¾ã™ï¼š

```text
PK=your_private_key_here
```

ãã®å¾Œã€ç’°å¢ƒå¤‰æ•°ã‚’èª­ã¿è¾¼ã¿ã¾ã™ï¼š

```terminal
# FROM: ./pol-upgrades

source .env;
```

### ã‚¹ãƒ†ãƒƒãƒ— 6ï¼šãƒˆãƒ¼ã‚¯ãƒ³ã¨ãƒ—ãƒ­ã‚­ã‚·ã®ãƒ‡ãƒ—ãƒ­ã‚¤

ã“ã“ã‹ã‚‰èˆˆå‘³æ·±ã„éƒ¨åˆ†ãŒå§‹ã¾ã‚Šã¾ã™ã€‚ä¸Šè¨˜ã®`DeployProxy`ã‚¹ã‚¯ãƒªãƒ—ãƒˆã«ãŠã„ã¦ã€OpenZeppelin ã®`Upgrades.deployUUPSProxy`ã®å‘¼ã³å‡ºã—ã¯ã€å®Ÿéš›ã«ã¯è£ã§ 2 ã¤ã®ã“ã¨ã‚’è¡Œã„ã¾ã™ï¼š

1. `DeFiToken`å®Ÿè£…ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹
2. `UUPSUpgradeable`ãƒ—ãƒ­ã‚­ã‚·ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã€ãã‚Œã‚’`DeFiToken`å®Ÿè£…ã«æ¥ç¶šã™ã‚‹

ã¾ãšã€ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã™ã‚‹ã“ã¨ã‹ã‚‰å§‹ã‚ã¾ã—ã‚‡ã†ï¼š

```terminal
# FROM: ./pol-upgrades

forge build;
```

æ¬¡ã«ã€ãƒ‡ãƒ—ãƒ­ã‚¤ãƒ¡ãƒ³ãƒˆã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’å®Ÿè¡Œã—ã¾ã™ï¼ˆä¸€è²«æ€§ã®ãŸã‚ã« Solidity ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚’å›ºå®šã—ã¦ã„ã¾ã™ï¼‰ï¼š

```terminal
# FROM: ./pol-upgrades

forge script script/DeployProxy.s.sol --broadcast --rpc-url https://bartio.rpc.berachain.com/ --private-key $PK --use 0.8.25;
```

![](https://super-translator.inaridiy.workers.dev/assets/image/76e08575-fde2-4ece-af27-729ea0215f02)
_Example Deployment Output_

### ã‚¹ãƒ†ãƒƒãƒ— 7ï¼šã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®æ¤œè¨¼ï¼ˆã‚ªãƒ—ã‚·ãƒ§ãƒ³ï¼‰

[Beratrail ã‚¨ã‚¯ã‚¹ãƒ—ãƒ­ãƒ¼ãƒ©ãƒ¼](https://bartio.beratrail.io/?ref=berachain.ghost.io)ã§ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’æ¤œè¨¼ã—ãŸã„å ´åˆã¯ã€ä»¥ä¸‹ã®ã‚ˆã†ã«å®Ÿè¡Œã—ã¾ã™ï¼š

```terminal
# FROM: ./pol-upgrades

forge verify-contract IMPLEMENTATION_ADDRESS ./src/DeFiTokenV1.sol:DeFiToken --verifier-url 'https://api.routescan.io/v2/network/testnet/evm/80084/etherscan' --etherscan-api-key "verifyContract" --num-of-optimizations 200 --compiler-version 0.8.25 --watch;
```

> `IMPLEMENTATION_ADDRESS`ã‚’ã‚¹ã‚¯ãƒªãƒ—ãƒˆå‡ºåŠ›ã§å¾—ã‚‰ã‚ŒãŸã‚¢ãƒ‰ãƒ¬ã‚¹ã«ç½®ãæ›ãˆã‚‹ã“ã¨ã‚’å¿˜ã‚Œãªã„ã§ãã ã•ã„ã€‚ãƒ—ãƒ­ã‚­ã‚·ã®æ¤œè¨¼ã«ã¤ã„ã¦ã¯å¿ƒé…ã™ã‚‹å¿…è¦ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚ã‚¨ã‚¯ã‚¹ãƒ—ãƒ­ãƒ¼ãƒ©ãƒ¼ã¯ã™ã§ã«ã“ã®ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«ã¤ã„ã¦èªè­˜ã—ã¦ã„ã‚‹ã‹ã‚‰ã§ã™ã€‚

ã“ã‚Œã§ã€Beratrail ã§ãƒ—ãƒ­ã‚­ã‚·ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ç¢ºèªã™ã‚‹ã¨ã€ERC20 ãƒˆãƒ¼ã‚¯ãƒ³ã®å±æ€§ãŒãƒ—ãƒ­ã‚­ã‚·ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«æ¥ç¶šã•ã‚Œã¦ã„ã‚‹ã®ãŒåˆ†ã‹ã‚Šã¾ã™ï¼ˆä¾‹ã¨ã—ã¦[ãƒ‡ãƒ—ãƒ­ã‚¤ã•ã‚ŒãŸãƒ—ãƒ­ã‚­ã‚·ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆ](https://bartio.beratrail.io/address/0xC9e6086507322ee8ae1D5c283101674588B342f5/contract/80084/readContract?ref=berachain.ghost.io)ã‚’å‚ç…§ã—ã¦ãã ã•ã„ï¼‰ã€‚

![](https://super-translator.inaridiy.workers.dev/assets/image/cd15a55f-ffaf-4d0d-8b85-b83a53a6d764)

### ã‚¹ãƒ†ãƒƒãƒ— 8ï¼šã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã•ã‚ŒãŸãƒˆãƒ¼ã‚¯ãƒ³ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ä½œæˆ

> ã‚³ãƒ¼ãƒ‰ä¾‹ã®ã¿ã‚’è¿½ã„ãŸã„å ´åˆã‚„ã€å®Ÿè£…ã—ã¦ã„ã‚‹ PoL ãƒ­ã‚¸ãƒƒã‚¯ã«èˆˆå‘³ãŒãªã„å ´åˆã¯ã€æ¬¡ã®ã‚³ãƒ¼ãƒ‰ãƒ–ãƒ­ãƒƒã‚¯ã¾ã§ã‚¹ã‚­ãƒƒãƒ—ã—ã¦ãã ã•ã„ã€‚

### PoL ã‚¤ãƒãƒ™ãƒ¼ã‚·ãƒ§ãƒ³ ğŸ’¡

ã“ã“ã‹ã‚‰ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ä½¿ã£ã¦å‰µé€ çš„ã«ãªã‚Œã¾ã™ã€‚ä¾‹ãˆã°ã€ã‚ãªãŸã®ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã«æœ€ã‚‚é•·ãé ã‘ã¦ã„ã‚‹ãƒ¦ãƒ¼ã‚¶ãƒ¼ã«ã‚ˆã‚Šå¤šãã®`$BGT`å ±é…¬ã‚’ä¸ãˆãŸã„ã¨ã—ã¾ã™ã€‚ã—ã‹ã—ã€é é‡‘ãƒˆãƒ¼ã‚¯ãƒ³ã«ã¯ã™ã§ã«ã‚¢ã‚¯ãƒ†ã‚£ãƒ–ãª[Reward Vault](https://docs.berachain.com/learn/pol/rewardvaults?ref=berachain.ghost.io)ãŒã‚ã‚Šã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ã«ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ç§»è¡Œã•ã›ãŸãã‚ã‚Šã¾ã›ã‚“ã€‚

ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½æ€§ãŒã“ã‚Œã‚’è§£æ±ºã—ã¾ã™ï¼ä¾‹ãˆã°ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ãŒå¾“æ¥ã®ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ãƒ‘ãƒ©ãƒ€ã‚¤ãƒ ã§ 100 ãƒˆãƒ¼ã‚¯ãƒ³ã‚’æŒã£ã¦ã„ã¦ã€æ™‚é–“ãƒ–ãƒ¼ã‚¹ãƒˆå‹ã‚·ã‚¹ãƒ†ãƒ ã«ç§»è¡Œã—ãŸã„ã¨ã—ã¾ã™ã€‚60 æ—¥é–“ãƒã‚¸ã‚·ãƒ§ãƒ³ã‚’ä¿æŒã—ãŸå¾Œã€PoL ç²å¾—ç‡ãŒå¾“æ¥ã®ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ã‚’ä¸Šå›ã‚‹ã“ã¨ã«æ°—ä»˜ãã§ã—ã‚‡ã†ã€‚
![](https://super-translator.inaridiy.workers.dev/assets/image/dbd142ec-2b81-457c-9920-38ce0aa39540)
_Traditional (V1) vs Time-boosted (V2) Rewards_

`src/DeFiTokenV2.sol`ã¨ã„ã†ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®å†…å®¹ã‚’è¨˜è¿°ã—ã¾ã™ï¼š

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import "@openzeppelin/contracts-upgradeable/token/ERC20/ERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";

interface IBerachainRewardsVault {
    function delegateStake(address account, uint256 amount) external;
    function delegateWithdraw(address account, uint256 amount) external;

    function getTotalDelegateStaked(
        address account
    ) external view returns (uint256);
}

/// @custom:oz-upgrades-from DeFiToken
contract DeFiTokenV2 is ERC20Upgradeable, OwnableUpgradeable, UUPSUpgradeable {
    IBerachainRewardsVault public rewardsVault;
    uint256 public constant BONUS_RATE = 50; // 50% bonus per 30 days
    uint256 public constant BONUS_PERIOD = 30 days;
    mapping(address => uint256) public lastBonusTimestamp;

    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    function initialize() public reinitializer(2) {
        __ERC20_init("DeFi Token V2", "DFTV2");
    }

    function setRewardsVault(address _rewardsVault) external onlyOwner {
        rewardsVault = IBerachainRewardsVault(_rewardsVault);
    }

    function mint(address to, uint256 amount) external onlyOwner {
        _mint(to, amount);
    }

    function applyBonus(address account) external {
        uint256 newBonusAmount = calculateBonus(account);
        require(newBonusAmount > 0, "No bonus to apply");

        // Mint new bonus tokens to this contract
        _mint(address(this), newBonusAmount);

        // Delegate new bonus stake
        rewardsVault.delegateStake(account, newBonusAmount);

        lastBonusTimestamp[account] = block.timestamp;
    }

    function calculateBonus(address account) public view returns (uint256) {
        uint256 userBalance = balanceOf(account);
        uint256 timeSinceLastBonus = block.timestamp -
            lastBonusTimestamp[account];
        return
            (userBalance * BONUS_RATE * timeSinceLastBonus) /
            (100 * BONUS_PERIOD);
    }

    function getBonusBalance(address account) public view returns (uint256) {
        return rewardsVault.getTotalDelegateStaked(account);
    }

    function removeBonus(address account) internal {
        uint256 bonusToRemove = getBonusBalance(account);
        if (bonusToRemove > 0) {
            rewardsVault.delegateWithdraw(account, bonusToRemove);
            _burn(address(this), bonusToRemove);
            lastBonusTimestamp[account] = 0;
        }
    }

    function transfer(
        address to,
        uint256 amount
    ) public override returns (bool) {
        removeBonus(msg.sender);
        lastBonusTimestamp[to] = block.timestamp;
        return super.transfer(to, amount);
    }

    function _authorizeUpgrade(
        address newImplementation
    ) internal override onlyOwner {}
}
```

ãªã‚‹ã»ã©ã€`DeFiTokenV2`ã«ã¯ã‹ãªã‚Šã®å¤‰æ›´ãŒã‚ã‚Šã¾ã™ã€‚æ–°ã—ã„å†…å®¹ã‚’ 2 ã¤ã®éƒ¨åˆ†ã«åˆ†ã‘ã¦èª¬æ˜ã—ã¾ã—ã‚‡ã†ã€‚ã¾ãšã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½æ€§ã«é–¢é€£ã™ã‚‹éƒ¨åˆ†ã‚’æŒ‡æ‘˜ã—ã€æ¬¡ã«æ–°ã—ã„ PoL ãƒ¡ã‚«ãƒ‹ã‚ºãƒ ã‚’ã‚µãƒãƒ¼ãƒˆã™ã‚‹ãŸã‚ã®ã‚¹ãƒãƒ¼ãƒˆã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆé–¢æ•°ã«ã¤ã„ã¦èª¬æ˜ã—ã¾ã™ã€‚

#### ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½æ€§ã®å¤‰æ›´ç‚¹

- ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«ã¯ OpenZeppelin ãŒè¦æ±‚ã™ã‚‹å…ƒã®`DeFiToken`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¸ã®å‚ç…§ãŒå«ã¾ã‚Œã¦ã„ã¾ã™ã€‚
- ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®`initialize`é–¢æ•°ã«ã¯`reinitializer(2)`ä¿®é£¾å­ãŒã‚ã‚Šã€ã“ã‚ŒãŒå†åˆæœŸåŒ–ã§ã‚ã‚‹ã“ã¨ã‚’ç¤ºã—ã¦ã„ã¾ã™ã€‚`2`ã¯ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ç•ªå·ã‚’æŒ‡ã—ã¾ã™ã€‚

> ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½æ€§ã®ã¿ã«é–¢å¿ƒãŒã‚ã‚‹å ´åˆã¯ã€æ¬¡ã®ã‚»ã‚¯ã‚·ãƒ§ãƒ³ã«ã‚¹ã‚­ãƒƒãƒ—ã—ã¦ãã ã•ã„ã€‚

#### PoL ã®å¤‰æ›´ç‚¹

ã“ã†æ€ã‚ã‚Œã‚‹ã‹ã‚‚ã—ã‚Œã¾ã›ã‚“ï¼š_å¤‰æ›´ã•ã‚Œãªã„ã¯ãšã® ERC20 ãƒã‚¸ã‚·ãƒ§ãƒ³ã«ç´ã¥ã„ã¦ã„ã‚‹ã®ã«ã€Reward Vault ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®æ®‹é«˜ãŒã©ã†ã‚„ã£ã¦å¢—ãˆã‚‹ã®ã ã‚ã†ï¼Ÿ_

ã„ã„è³ªå•ã§ã™ï¼æ™‚é–“ãƒ–ãƒ¼ã‚¹ãƒˆå‹å ±é…¬ã‚’å®Ÿç¾ã™ã‚‹ãŸã‚ã«ã€Reward Vault ã®`delegateStake`æ©Ÿèƒ½ã‚’æ´»ç”¨ã—ã¦ã„ã¾ã™ã€‚ã“ã‚Œã«ã‚ˆã‚Šã€ã‚¹ãƒãƒ¼ãƒˆã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆãŒãƒ¦ãƒ¼ã‚¶ãƒ¼ã«ä»£ã‚ã£ã¦ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ã§ãã¾ã™ã€‚ã“ã‚Œã¯ã€ã“ã“ã§ã®æ™‚é–“ãƒ–ãƒ¼ã‚¹ãƒˆå‹å ±é…¬ã‚„ã€[ä»®æƒ³/é ERC20 ãƒã‚¸ã‚·ãƒ§ãƒ³ã‚’ PoL ã¨çµ±åˆã™ã‚‹](https://docs.berachain.com/developers/guides/advanced-pol?ref=berachain.ghost.io#description-of-approach)ãªã©ã€æ§˜ã€…ãªãƒ¦ãƒ¼ã‚¹ã‚±ãƒ¼ã‚¹ã«å½¹ç«‹ã¡ã¾ã™ã€‚

æ™‚é–“ãƒ™ãƒ¼ã‚¹ã®ãƒ­ã‚¸ãƒƒã‚¯ã‚’é©ç”¨ã™ã‚‹ãŸã‚ã«ã€`DeFiTokenV2`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã¯ä»Šã‚„ãƒ¦ãƒ¼ã‚¶ãƒ¼ã®ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ãƒ­ã‚¸ãƒƒã‚¯ã‚’å‡¦ç†ã—ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ã¯å˜ã«ã‚¦ã‚©ãƒ¬ãƒƒãƒˆã«ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ä¿æŒã™ã‚‹ã ã‘ã§ã™ã€‚è©³ã—ãè¦‹ã¦ã„ãã¾ã—ã‚‡ã†ï¼

- `setRewardsVault`ã¯ã€ãƒ—ãƒ­ãƒˆã‚³ãƒ«ãŒãƒ¦ãƒ¼ã‚¶ãƒ¼ã®ãƒ–ãƒ¼ã‚¹ãƒˆã‚’ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ã—ã¦`$BGT`ã‚’ç²å¾—ã™ã‚‹ Reward Vault ã‚¢ãƒ‰ãƒ¬ã‚¹ã‚’è¨­å®šã§ãã‚‹ã‚ˆã†ã«ã—ã¾ã™ã€‚
- `calculateBonus`ã¯ã€æœ€å¾Œã«ãƒœãƒ¼ãƒŠã‚¹ãŒé©ç”¨ã•ã‚Œã¦ã‹ã‚‰ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ã«ä¸ãˆã‚‰ã‚Œã‚‹è¿½åŠ æ®‹é«˜ã‚’è¨ˆç®—ã—ã¾ã™ã€‚
- `applyBonus`ã¯æ–°ã—ã„ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ç™ºè¡Œã—ã€ç‰¹å®šã®ãƒ¦ãƒ¼ã‚¶ãƒ¼ã«ä»£ã‚ã£ã¦`delegateStake`ã‚’ä½¿ç”¨ã—ã¦ã‚¹ãƒ†ãƒ¼ã‚­ãƒ³ã‚°ã—ã¾ã™ã€‚ã“ã‚Œã¯ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ãŒç²å¾—ã—ãŸãƒœãƒ¼ãƒŠã‚¹ã«åŸºã¥ã„ã¦è¡Œã‚ã‚Œã¾ã™ã€‚
- `getBonusBalance`ã¯ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ã®ãƒœãƒ¼ãƒŠã‚¹æ®‹é«˜ã‚’ Rewards Vault ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã«å•ã„åˆã‚ã›ã¾ã™ã€‚
- `removeBonus`ã¯ã€Reward Vault ã®`delegateWithdraw`é–¢æ•°ã‚’å‘¼ã³å‡ºã—ã¦ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ã®ãƒœãƒ¼ãƒŠã‚¹æ®‹é«˜ã‚’å¼•ãå‡ºã—/ç„¡åŠ¹åŒ–ã—ã€ãã®ãƒˆãƒ¼ã‚¯ãƒ³ã‚’ç„¼å´ã—ã¾ã™ã€‚ã“ã‚Œã¯ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼ãŒãƒˆãƒ¼ã‚¯ãƒ³ã‚’è»¢é€ã—ãŸéš›ã®ãƒœãƒ¼ãƒŠã‚¹ã®æå¤±ã‚’åæ˜ ã—ã¾ã™ã€‚

ã“ã®ã‚µãƒ³ãƒ—ãƒ«ã§ã€å¾“æ¥ã® PoL ã«ä»£ã‚ã‚‹é©æ–°çš„ãªé¸æŠè‚¢ãŒã©ã®ã‚ˆã†ãªã‚‚ã®ã‹ã€ã‚¤ãƒ¡ãƒ¼ã‚¸ãŒã¤ã‹ã‚ãŸã¨æ€ã„ã¾ã™ã€‚**_ã“ã®ã‚³ãƒ¼ãƒ‰ã¯ä¸å®Œå…¨ã§ã€æœ¬ç•ªç’°å¢ƒã§ã®ä½¿ç”¨ã«ã¯é©ã•ãªã„ã“ã¨ã«æ³¨æ„ã—ã¦ãã ã•ã„_**ã€‚

### ã‚¹ãƒ†ãƒƒãƒ— 9ï¼šã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ãƒ†ã‚¹ãƒˆ

ã™ã¹ã¦ã®ã‚¹ãƒãƒ¼ãƒˆã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’è¨˜è¿°ã—ãŸã®ã§ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰å¯èƒ½ãª PoL çµ±åˆãƒˆãƒ¼ã‚¯ãƒ³ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®å‹•ä½œã‚’ãƒ†ã‚¹ãƒˆã—ã¾ã—ã‚‡ã†ã€‚

ä»¥ä¸‹ã®æ©Ÿèƒ½ã‚’ãƒ†ã‚¹ãƒˆã—ã¾ã™ï¼š

- ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ãŒæ­£å¸¸ã«å®Ÿè¡Œã•ã‚Œã‚‹ã“ã¨ã®ç¢ºèª
- PoL ãƒœãƒ¼ãƒŠã‚¹ãƒ­ã‚¸ãƒƒã‚¯ãŒæœŸå¾…é€šã‚Šã«æ©Ÿèƒ½ã™ã‚‹ã“ã¨ã®ç¢ºèª

`test/DeFiToken.t.sol`ã¨ã„ã†ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®å†…å®¹ã‚’è¨˜è¿°ã—ã¾ã™ï¼š

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import "forge-std/Test.sol";
import "../src/DeFiTokenV1.sol";
import "../src/DeFiTokenV2.sol";
import "forge-std/console.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Utils.sol";
import {Upgrades} from "openzeppelin-foundry-upgrades/Upgrades.sol";

contract MockRewardsVault {
    mapping(address => uint256) public delegatedStakes;

    function delegateStake(address account, uint256 amount) external {
        delegatedStakes[account] += amount;
    }

    function delegateWithdraw(address account, uint256 amount) external {
        require(
            delegatedStakes[account] >= amount,
            "Insufficient delegated stake"
        );
        delegatedStakes[account] -= amount;
    }

    function getTotalDelegateStaked(
        address account
    ) external view returns (uint256) {
        return delegatedStakes[account];
    }
}

contract DeFiTokenTest is Test {
    DeFiToken deFiToken;
    DeFiTokenV2 deFiTokenV2;
    ERC1967Proxy proxy;
    address owner;
    address user1;
    MockRewardsVault mockRewardsVault;

    function setUp() public {
        DeFiToken implementation = new DeFiToken();
        owner = vm.addr(1);
        user1 = vm.addr(2);

        vm.startPrank(owner);
        proxy = new ERC1967Proxy(
            address(implementation),
            abi.encodeCall(implementation.initialize, owner)
        );
        deFiToken = DeFiToken(address(proxy));
        vm.stopPrank();

        mockRewardsVault = new MockRewardsVault();
    }

    function testBoostedStakingFunctionality() public {
        testUpgradeToV2();

        vm.startPrank(owner);
        deFiTokenV2.setRewardsVault(address(mockRewardsVault));
        deFiTokenV2.mint(user1, 1000 * 1e18);
        vm.stopPrank();

        // Fast forward 15 days
        vm.warp(block.timestamp + 15 days);

        // Apply bonus for user1
        vm.prank(user1);
        deFiTokenV2.applyBonus(user1);

        // Check bonus balance (should be 25% of user's balance after 15 days)
        uint256 expectedBonus = (1000 * 1e18 * 25) / 100;
        assertApproxEqAbs(
            deFiTokenV2.getBonusBalance(user1),
            expectedBonus,
            1e15
        );

        // Fast forward another 30 days
        vm.warp(block.timestamp + 30 days);

        // Apply bonus again (should be 75% of user's balance)
        vm.prank(user1);
        deFiTokenV2.applyBonus(user1);
        expectedBonus = (1000 * 1e18 * 75) / 100;
        assertApproxEqAbs(
            deFiTokenV2.getBonusBalance(user1),
            expectedBonus,
            1e15
        );

        // Test bonus removal on transfer
        vm.prank(user1);
        deFiTokenV2.transfer(owner, 500 * 1e18);

        // Check that bonus is removed
        assertEq(deFiTokenV2.getBonusBalance(user1), 0);
    }

    function testUpgradeToV2() public {
        vm.startPrank(owner);
        Upgrades.upgradeProxy(
            address(proxy),
            "DeFiTokenV2.sol:DeFiTokenV2",
            abi.encodeCall(DeFiTokenV2.initialize, ())
        );
        vm.stopPrank();

        deFiTokenV2 = DeFiTokenV2(address(proxy));
        assertTrue(address(deFiTokenV2) == address(proxy));
    }
}
```

ã¯ã„ã€ãƒ†ã‚¹ãƒˆã‚³ãƒ¼ãƒ‰ã¯é•·ã„ã§ã™ãŒã€ã‚³ãƒ¡ãƒ³ãƒˆã‚’èª­ã‚€ã“ã¨ã§å„ãƒ†ã‚¹ãƒˆã‚±ãƒ¼ã‚¹ã§ä½•ãŒèµ·ã“ã£ã¦ã„ã‚‹ã‹ã‚’ç†è§£ã§ãã‚‹ã¯ãšã§ã™ã€‚

```terminal
# FROM: ./pol-upgrades

forge clean && forge test;

# [EXAMPLE OUTPUT]:
# Ran 2 tests for test/DeFiToken.t.sol:DeFiTokenTest
# [PASS] testBoostedStakingFunctionality() (gas: 2032978)
# [PASS] testUpgradeToV2() (gas: 1904385)
# Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 5.62s (11.14s CPU time)

# Ran 1 test suite in 5.66s (5.62s CPU time): 2 tests passed, 0 failed, 0 skipped (2 total tests)
```

### ã‚¹ãƒ†ãƒƒãƒ— 10ï¼šã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã‚¹ã‚¯ãƒªãƒ—ãƒˆã®ä½œæˆ

ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆãŒæ­£å¸¸ã«å‹•ä½œã™ã‚‹ã“ã¨ã‚’ç¢ºèªã—ãŸã®ã§ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã®æº–å‚™ã‚’ã—ã¾ã—ã‚‡ã†ã€‚`script/DeployUpgrade.s.sol`ã¨ã„ã†ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®å†…å®¹ã‚’è¨˜è¿°ã—ã¾ã™ï¼š

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import "../src/DeFiTokenV2.sol";
import "forge-std/Script.sol";
import {Upgrades} from "openzeppelin-foundry-upgrades/Upgrades.sol";

contract DeployAndUpgrade is Script {
    function run() public {
        // Replace with your proxy address
        address proxy = 0x0000000000000000000000000000000000000000;
        vm.startBroadcast();

        Upgrades.upgradeProxy(
            proxy,
            "DeFiTokenV2.sol:DeFiTokenV2",
            abi.encodeCall(DeFiTokenV2.initialize, ())
        );

        vm.stopBroadcast();

        console.log("Token Name:", DeFiTokenV2(proxy).name());
    }
}
```

> `proxy`å¤‰æ•°ã‚’ã‚¹ãƒ†ãƒƒãƒ— 6 ã§å¾—ãŸãƒ—ãƒ­ã‚­ã‚·ã‚¢ãƒ‰ãƒ¬ã‚¹ã«ç½®ãæ›ãˆã¦ãã ã•ã„ã€‚

ã“ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆã¯ã€ä»¥ä¸‹ã®æ‰‹é †ã§ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ãƒ—ãƒ­ã‚»ã‚¹ã‚’çµ„ã¿ç«‹ã¦ã¦ã„ã¾ã™ï¼š

1. ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã•ã‚ŒãŸ`DeFiTokenV2`ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹
2. ãƒ—ãƒ­ã‚­ã‚·ã®å®Ÿè£…ã‚’æ–°ã—ã„`DeFiTokenV2`ã«åˆ‡ã‚Šæ›¿ãˆã‚‹
3. `initialize`ã‚’å†åº¦å‘¼ã³å‡ºã—ã¦ãƒˆãƒ¼ã‚¯ãƒ³ã®åå‰ã‚’å¤‰æ›´ã™ã‚‹

### ã‚¹ãƒ†ãƒƒãƒ— 11ï¼šã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã®å®Ÿè¡Œ

ãƒ“ãƒ«ãƒ‰ã‚¢ãƒ¼ãƒ†ã‚£ãƒ•ã‚¡ã‚¯ãƒˆã‚’ã‚¯ãƒªãƒ¼ãƒ³ã«ã—ã¦ã‹ã‚‰ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’å®Ÿè¡Œã—ã¾ã™ï¼š

```terminal
# FROM: ./pol-upgrades

forge clean;
forge script script/DeployUpgrade.s.sol --broadcast --rpc-url https://bartio.rpc.berachain.com/ --private-key $PK --use 0.8.25;
```

Beratrail ã‚¨ã‚¯ã‚¹ãƒ—ãƒ­ãƒ¼ãƒ©ãƒ¼ã§ãƒ—ãƒ­ã‚­ã‚·ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã‚’ç¢ºèªã—ã¦ã¿ã¦ãã ã•ã„ã€‚åŒã˜ã‚¢ãƒ‰ãƒ¬ã‚¹ã§ã€ãƒˆãƒ¼ã‚¯ãƒ³åï¼ˆé€šå¸¸ã¯ä¸å¤‰ã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ï¼‰ãŒå¤‰æ›´ã•ã‚Œã¦ã„ã‚‹ã®ãŒè¦‹ãˆã‚‹ã¯ãšã§ã™ï¼

![](https://super-translator.inaridiy.workers.dev/assets/image/46d622bd-65d3-4427-8c8b-397df3c5aad7)

ãƒˆãƒ¼ã‚¯ãƒ³åãŒå¤‰æ›´ã•ã‚Œã¾ã—ãŸï¼

## ã¾ã¨ã‚

ãŠã‚ã§ã¨ã†ã”ã–ã„ã¾ã™ï¼ã“ã“ã¾ã§åˆ°é”ã—ãŸã‚ãªãŸã¯ã€ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆãŒã©ã®ã‚ˆã†ã«æ©Ÿèƒ½ã—ã€ã©ã®ã‚ˆã†ã«ã‚ãªãŸã®åˆ©ç‚¹ã¨ãªã‚‹ã‹ã®åŸºæœ¬ã‚’å­¦ã‚“ã ã“ã¨ã«ãªã‚Šã¾ã™ã€‚ã•ã‚‰ã«æ³¨æ„æ·±ãè¦‹ã¦ã„ãŸå ´åˆã¯ã€Proof-of-Liquidity ã‚’æ‰±ã†é©æ–°çš„ãªæ–¹æ³•ã«ã¤ã„ã¦ã‚‚å­¦ã‚“ã ã“ã¨ã§ã—ã‚‡ã†ã€‚
ã“ã®ã‚¬ã‚¤ãƒ‰ã«å¾“ã†ã“ã¨ã§ã€Berachain ä¸Šã«ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ€ãƒ–ãƒ«ãª ERC20 ãƒˆãƒ¼ã‚¯ãƒ³ã‚’æ­£å¸¸ã«ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã¾ã—ãŸã€‚ãã—ã¦ã€ãƒ—ãƒ­ã‚­ã‚·ã‚³ãƒ³ãƒˆãƒ©ã‚¯ãƒˆã®å®Ÿè£…ã‚’ã‚¢ãƒƒãƒ—ã‚°ãƒ¬ãƒ¼ãƒ‰ã—ã¦ã€ãã®æ©Ÿèƒ½ã«ã‚¯ãƒ¼ãƒ«ãªå¤‰æ›´ã‚’åŠ ãˆã‚‹ã“ã¨ã«æˆåŠŸã—ã¾ã—ãŸ ğŸ‰

---

### ğŸ» Full Code Repository

æœ€çµ‚çš„ãªã‚³ãƒ¼ãƒ‰ã‚„ä»–ã®ã‚¬ã‚¤ãƒ‰ã‚’è¦‹ãŸã„å ´åˆã¯ã€[**_Berachain Upgradeable Contracts Guide Code_**](https://github.com/berachain/guides/tree/main/apps/openzeppelin-upgrades?ref=berachain.ghost.io)ã‚’å‚ç…§ã—ã¦ãã ã•ã„ã€‚
[**_Berachain GitHub Guides Repo_**](https://github.com/berachain/guides?ref=berachain.ghost.io)
https://github.com/berachain/guides/tree/main/apps/pyth-oracle?ref=berachain.ghost.io

### ğŸ› ï¸ ã•ã‚‰ã«é–‹ç™ºã‚’é€²ã‚ãŸã„ã§ã™ã‹ï¼Ÿ

Berachain ã§ã•ã‚‰ã«é–‹ç™ºã‚’é€²ã‚ãŸã„ã€ã‚ˆã‚Šå¤šãã®å®Ÿè£…ä¾‹ã‚’è¦‹ãŸã„ã¨ãŠè€ƒãˆã§ã™ã‹ï¼Ÿç§ãŸã¡[**_Berachain GitHub Guides Repo_**](https://github.com/berachain/guides?ref=berachain.ghost.io) ã‚’ãœã²ã”è¦§ãã ã•ã„ã€‚ã“ã®ãƒªãƒã‚¸ãƒˆãƒªã«ã¯ã€NextJSã€Hardhatã€Viemã€Foundry ãªã©ã€æ§˜ã€…ãªæŠ€è¡“ã‚’ä½¿ç”¨ã—ãŸå¹…åºƒã„å®Ÿè£…ä¾‹ãŒç”¨æ„ã•ã‚Œã¦ã„ã¾ã™ã€‚

https://github.com/berachain/guides/tree/main?ref=berachain.ghost.io

ã‚ˆã‚Šè©³ç´°ãªæƒ…å ±ã‚’æ¢æ±‚ã—ãŸã„å ´åˆã¯ã€[**_Berachain ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆ_**](https://docs.berachain.com/?ref=berachain.ghost.io)ã‚’ã”è¦§ãã ã•ã„ã€‚

https://docs.berachain.com/?ref=berachain.ghost.io

### é–‹ç™ºè€…ã‚µãƒãƒ¼ãƒˆã‚’ãŠæ¢ã—ã§ã™ã‹ï¼Ÿ

è³ªå•ã‚’ã™ã‚‹ãŸã‚ã«ã€[**_Berachain Discord_**](https://discord.com/invite/berachain?ref=berachain.ghost.io)ã‚µãƒ¼ãƒãƒ¼ã«å‚åŠ ã—ã€é–‹ç™ºè€…ãƒãƒ£ãƒ³ãƒãƒ«ã‚’ãƒã‚§ãƒƒã‚¯ã—ã¦ãã ã•ã„ã€‚

â¤ï¸ ã“ã®è¨˜äº‹ã«å¯¾ã—ã¦æ„›ã‚’ç¤ºã™ã®ã‚’å¿˜ã‚Œãªã„ã§ãã ã•ã„ ğŸ‘ğŸ¼

---

---

:::note info
ã€Sunrise ã¨ã¯ã€‘
Sunrise ã¯ Proof of Liquidity(PoL)ã¨ Fee Abstractionï¼ˆæ‰‹æ•°æ–™æŠ½è±¡åŒ–ï¼‰ã‚’å‚™ãˆãŸãƒ‡ãƒ¼ã‚¿å¯ç”¨æ€§ãƒ¬ã‚¤ãƒ¤ãƒ¼ã§ã™ã€‚ ç§ãŸã¡ã¯ DA ã®ä½“é¨“ã‚’å†æ§‹ç¯‰ã—ã€å¤šæ§˜ãªã‚¨ã‚³ã‚·ã‚¹ãƒ†ãƒ ã‹ã‚‰ã®ãƒ¢ã‚¸ãƒ¥ãƒ©ãƒ¼å‹æµå‹•æ€§ã‚’æ´»ç”¨ã—ã¦ãƒ­ãƒ¼ãƒ«ã‚¢ãƒƒãƒ—ã‚’ç«‹ã¡ä¸Šã’ã¦ã„ã¾ã™ã€‚

- [Sunrise - Specialized DA Layer for Proof of Liquidity](https://sunriselayer.io/)
- [Sunrise / Gluon Docs](https://docs.sunriselayer.io/)

ã€Social Linksã€‘

- [X(æ—§ Twitter)[è‹±èª])](https://twitter.com/SunriseLayer)
- [X(æ—§ Twitter)[æ—¥æœ¬èª])](https://twitter.com/SunriseLayer)
- [Discord Community](https://discord.com/invite/sunrise)
- [Medium](https://sunriselayer.medium.com/)
- [Github](https://github.com/sunriselayer)
- [Linkedin](https://www.linkedin.com/company/sunriselayer)

ã€ãŠå•åˆã›ã€‘
Sunrise ã¸ã®ãŠå•ã„åˆã‚ã›ã¯ã“ã¡ã‚‰ã‹ã‚‰ ğŸ‘‰ [Google Form](https://forms.gle/h8RVahxRtXUvYwnv6)

![1080x360.jpeg](https://qiita-image-store.s3.ap-northeast-1.amazonaws.com/0/3839047/8971d83b-3331-4757-dc72-320d28618735.jpeg)

:::
